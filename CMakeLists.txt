project (FlexiBLAS)
ENABLE_LANGUAGE (Fortran)
cmake_minimum_required(VERSION 2.6)
SET(CMAKE_MODULE_PATH ${CMAKE_MODULE_PATH} ${PROJECT_SOURCE_DIR}/CMakeModules)

OPTION(TESTS "Enable Tests" ON)
OPTION(CBLAS "Enable CBLAS Interface" ON ) 
OPTION(INTEGER8 "Use 8 Byte Integer" OFF) 
OPTION(PROFILE "Enable BLAS profiling" OFF) 
OPTION(BUILD_SHARED_LIBS "Enable Shared Library" ON) 
#SET(BUILD_SHARED_LIBS ON)
SET(LIBS m) 

# Paths 
INCLUDE(GNUInstallDirs)
IF(DEFINED SYSCONFDIR)
 SET(CMAKE_INSTALL_FULL_SYSCONFDIR ${SYSCONFDIR})
ENDIF()
MESSAGE(STATUS "SYSCONFDIR: ${CMAKE_INSTALL_FULL_SYSCONFDIR}")
MESSAGE(STATUS "LIBDIR:     ${CMAKE_INSTALL_FULL_LIBDIR}")
MESSAGE(STATUS "BINDIR:     ${CMAKE_INSTALL_FULL_BINDIR}")
MESSAGE(STATUS "INCLUDEIR:  ${CMAKE_INSTALL_FULL_INCLUDEDIR}")

#
# build type
# 
IF( NOT CMAKE_BUILD_TYPE )
	SET( CMAKE_BUILD_TYPE "Release" )
ENDIF( NOT CMAKE_BUILD_TYPE )

IF ( CMAKE_BUILD_TYPE STREQUAL "Debug" )
	#	SET (CMAKE_C_FLAGS_DEBUG ${CMAKE_C_FLAGS_DEBUG} -pg)
	SET ( DEBUG "ON" )
ENDIF ( CMAKE_BUILD_TYPE STREQUAL "Debug" )

IF (DEBUG STREQUAL ON) 
	SET( CMAKE_BUILD_TYPE "Debug" )
	SET ( DEBUG TRUE)
ENDIF (DEBUG STREQUAL ON )

IF ( INTEGER8 STREQUAL ON ) 
	IF ( ${CMAKE_C_COMPILER_ID} STREQUAL "GNU") 
		MESSAGE(STATUS "Setting GNU Compiler Flags")
		SET(CMAKE_Fortran_FLAGS "${CMAKE_Fortran_FLAGS} -fdefault-integer-8")
	ELSEIF (${CMAKE_C_COMPILER_ID} STREQUAL "Intel" ) 
		SET ( CMAKE_Fortran_FLAGS "${CMAKE_Fortran_FLAGS} -i8") 
	ENDIF()
ENDIF (INTEGER8 STREQUAL ON) 

IF (PROFILE STREQUAL ON)
	ADD_DEFINITIONS(-DFLEXIBLAS_PROFILE)
ENDIF()
IF (CBLAS STREQUAL ON) 
	ADD_DEFINITIONS(-DFLEXIBLAS_CBLAS)
ENDIF()
IF(PROFILE STREQUAL ON ) 
	SET ( FLEXIBLAS_DEFAULT_LIB_PATH "${CMAKE_INSTALL_FULL_LIBDIR}/flexiblas-profile/:${CMAKE_BINARY_DIR}/netlib:${CMAKE_BINARY_DIR}/src")

ELSE()
	SET ( FLEXIBLAS_DEFAULT_LIB_PATH "${CMAKE_INSTALL_FULL_LIBDIR}/flexiblas/:${CMAKE_BINARY_DIR}/netlib:${CMAKE_BINARY_DIR}/src")
#SET ( FLEXIBLAS_DEFAULT_LIB_PATH "${CMAKE_BINARY_DIR}/src:${CMAKE_INSTALL_PREFIX}/lib/flexiblas/:${CMAKE_BINARY_DIR}/netlib")
ENDIF()
#IF (NOT SYSCONFDIR) 
#	SET(CMAKE_INSTALL_FULL_SYSCONFDIR "${CMAKE_INSTALL_PREFIX}/etc/")
#ENDIF()


SET ( INCLUDE_DIR ${INCLUDE_DIR} ${CMAKE_BINARY_DIR}/include ${CMAKE_SOURCE_DIR}/include ${CMAKE_BINARY_DIR} )
CONFIGURE_FILE(${CMAKE_SOURCE_DIR}/config.h.in ${CMAKE_BINARY_DIR}/include/flexiblas_config.h)
LIST(REMOVE_DUPLICATES INCLUDE_DIR)
IF (APPLE) 
	SET ( CMAKE_C_FLAGS "${CMAKE_C_FLAGS} -D_FILE_OFFSET_BITS=64 -fPIC") 
ELSE() 
	SET ( CMAKE_C_FLAGS "${CMAKE_C_FLAGS} -D_FILE_OFFSET_BITS=64 -rdynamic -Wall -fPIC ")
ENDIF()
SET ( CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -D_FILE_OFFSET_BITS=64 -fPIC") 

SET(CMAKE_INSTALL_RPATH_USE_LINK_PATH TRUE)


#
# Check Some files 
#
INCLUDE (CheckIncludeFiles)
# CHECK_INCLUDE_FILES (stdatomic.h HAVE_STDATOMIC_H)
CHECK_INCLUDE_FILES (dlfcn.h	 HAVE_DLFCN_H)

INCLUDE (CheckSymbolExists) 
CHECK_SYMBOL_EXISTS(RTLD_GLOBAL   "dlfcn.h" HAVE_RTLD_GLOBAL)
CHECK_SYMBOL_EXISTS(RTLD_DEEPBIND "dlfcn.h" HAVE_RTLD_DEEPBIND)
CHECK_SYMBOL_EXISTS(RTLD_NOW	  "dlfcn.h" HAVE_RTLD_NOW)

INCLUDE (CheckFunctionExists) 
CHECK_FUNCTION_EXISTS(dlopen DLOPEN_C) 
SET(__CMAKE_REQUIRED_LIBRARIES ${CMAKE_REQUIRED_LIBRARIES})
SET(CMAKE_REQUIRED_LIBRARIES dl)
CHECK_FUNCTION_EXISTS(dlopen DLOPEN_DL)
SET(CMAKE_REQUIRED_LIBRARIES ${__CMAKE_REQUIRED_LIBRARIES})

IF (DLOPEN_DL) 
 SET(LIBS ${LIBS} dl)
ENDIF()
IF (NOT ( DLOPEN_DL OR DLOPEN_C ) ) 
 MESSAGE(ERROR "No dlopen function found")
ENDIF()

IF ( NOT HAVE_DLFCN_H ) 
	MESSAGE(FATAL_ERROR "dlfcn.h not found, aborting")
ENDIF()

SET(EXTRA_BLAS "")
add_subdirectory(netlib)
add_subdirectory(src)
add_subdirectory(examples)

IF(PROFILE STREQUAL ON)
	INSTALL(FILES	${CMAKE_SOURCE_DIR}/include/f77blas_interface.h DESTINATION ${CMAKE_INSTALL_FULL_INCLUDEDIR}/flexiblas-profile/)
	IF(CBLAS STREQUAL ON) 
		INSTALL(FILES	${CMAKE_SOURCE_DIR}/include/cblas.h DESTINATION ${CMAKE_INSTALL_FULL_INCLUDEDIR}/flexiblas-profile/)
	ENDIF()
ElSE()
	INSTALL(FILES	${CMAKE_SOURCE_DIR}/include/f77blas_interface.h DESTINATION ${CMAKE_INSTALL_FULL_INCLUDEDIR}/flexiblas/)
	IF(CBLAS STREQUAL ON) 
		INSTALL(FILES	${CMAKE_SOURCE_DIR}/include/cblas.h DESTINATION ${CMAKE_INSTALL_FULL_INCLUDEDIR}/flexiblas/)
	ENDIF()
ENDIF()


CONFIGURE_FILE(
  "${CMAKE_CURRENT_SOURCE_DIR}/cmake_uninstall.cmake.in"
  "${CMAKE_CURRENT_BINARY_DIR}/cmake_uninstall.cmake"
  IMMEDIATE @ONLY)
ADD_CUSTOM_TARGET(uninstall "${CMAKE_COMMAND}" -P "${CMAKE_CURRENT_BINARY_DIR}/cmake_uninstall.cmake") 

set(prefix ${CMAKE_INSTALL_PREFIX})
set(libdir ${CMAKE_INSTALL_FULL_LIBDIR})
set(PKG_CONFIG_DIR ${CMAKE_INSTALL_FULL_LIBDIR}/pkgconfig)
if (PROFILE STREQUAL ON)  
	set(includedir ${CMAKE_INSTALL_PREFIX}/include/flexiblas-profile/)
	set(flexiblasname flexiblas-profile)
	CONFIGURE_FILE(${CMAKE_CURRENT_SOURCE_DIR}/flexiblas.pc.in ${CMAKE_CURRENT_BINARY_DIR}/flexiblas-profile.pc)
	install(FILES
		  ${CMAKE_CURRENT_BINARY_DIR}/flexiblas-profile.pc
		  DESTINATION ${PKG_CONFIG_DIR}
		 )
else()
	set(includedir ${CMAKE_INSTALL_PREFIX}/include/flexiblas/)
	set(flexiblasname flexiblas)
	CONFIGURE_FILE(${CMAKE_CURRENT_SOURCE_DIR}/flexiblas.pc.in ${CMAKE_CURRENT_BINARY_DIR}/flexiblas.pc)
	install(FILES
		  ${CMAKE_CURRENT_BINARY_DIR}/flexiblas.pc
		  DESTINATION ${PKG_CONFIG_DIR}
   	)
endif()
   
IF ( TESTS ) 
	MESSAGE( STATUS "Enable Testing...")
	ENABLE_TESTING()
	add_subdirectory(test)
ENDIF (TESTS)

#
# Final Status
#
MESSAGE(STATUS "------------------------------------------------")
MESSAGE(STATUS "      Final Configuration Status ")
MESSAGE(STATUS "------------------------------------------------")
MESSAGE(STATUS "Build 8 byte integer: ${INTEGER8}")
MESSAGE(STATUS "Build CBLAS interface: ${CBLAS}")
MESSAGE(STATUS "Install path: ${CMAKE_INSTALL_PREFIX}")
MESSAGE(STATUS "Extra BLAS search path: ${HBLAS_DEFAULT_LIB_PATH}")
MESSAGE(STATUS "Interface to Extra BLAS Libraries:")
FOREACH(EBLAS ${EXTRA_BLAS})
MESSAGE(STATUS " - ${EBLAS}")
ENDFOREACH()
MESSAGE(STATUS "------------------------------------------------")


