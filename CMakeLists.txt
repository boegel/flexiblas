project (FlexiBLAS)
ENABLE_LANGUAGE (Fortran)
cmake_minimum_required(VERSION 2.8)
IF (${CMAKE_MAJOR_VERSION} GREATER "2") 
	MESSAGE(STATUS "Using CMAKE below 3.0.0 setting CMP0026 to old") 
	cmake_policy(SET CMP0026 OLD) 
ENDIF() 

SET(CMAKE_MODULE_PATH ${CMAKE_MODULE_PATH} ${PROJECT_SOURCE_DIR}/CMakeModules)

# cmake_policy(SET CMP0026 OLD)

OPTION(TESTS "Enable Tests" ON)
OPTION(CBLAS "Enable CBLAS Interface" ON ) 
OPTION(INTEGER8 "Use 8 Byte Integer" OFF) 
OPTION(EXTBLAS "Add BLAS extensions like AXPBY" ON) 
OPTION(BUILD_SHARED_LIBS "Enable Shared Library" ON) 
OPTION(NETLIB64 "Build 64 Bit Netlib (not supported on 32bit systems)" OFF)
OPTION(DEV "Development Build" OFF) 
OPTION(MKL_CUSTOM "Build MKL support with Intel MKLs Custom library builder" OFF) 
#SET(BUILD_SHARED_LIBS ON)
SET(LIBS m) 

# Paths 
INCLUDE(GNUInstallDirs)
IF(DEFINED SYSCONFDIR)
 SET(CMAKE_INSTALL_FULL_SYSCONFDIR ${SYSCONFDIR})
ENDIF()
MESSAGE(STATUS "SYSCONFDIR: ${CMAKE_INSTALL_FULL_SYSCONFDIR}")
MESSAGE(STATUS "LIBDIR:     ${CMAKE_INSTALL_FULL_LIBDIR}")
MESSAGE(STATUS "BINDIR:     ${CMAKE_INSTALL_FULL_BINDIR}")
MESSAGE(STATUS "INCLUDEDIR: ${CMAKE_INSTALL_FULL_INCLUDEDIR}")
MESSAGE(STATUS "MANDIR:     ${CMAKE_INSTALL_FULL_MANDIR}")

#
# build type
# 
IF( NOT CMAKE_BUILD_TYPE )
	SET( CMAKE_BUILD_TYPE "Release" )
ENDIF( NOT CMAKE_BUILD_TYPE )

IF ( CMAKE_BUILD_TYPE STREQUAL "Debug" )
	#	SET (CMAKE_C_FLAGS_DEBUG ${CMAKE_C_FLAGS_DEBUG} -pg)
	SET ( DEBUG "ON" )
ENDIF ( CMAKE_BUILD_TYPE STREQUAL "Debug" )

IF ( ${CMAKE_C_COMPILER_ID} STREQUAL "Intel")
	SET( CMAKE_C_FLAGS "${CMAKE_C_FLAGS} -Wall -fPIC")
	SET( CMAKE_Fortran_FLAGS "${CMAKE_Fortran_FLAGS} -fPIC -recursive")
ELSE()
	SET( CMAKE_C_FLAGS "${CMAKE_C_FLAGS} -Wall -fPIC")
	SET( CMAKE_Fortran_FLAGS "${CMAKE_Fortran_FLAGS} -Wall -fPIC -frecursive")
ENDIF()

IF (DEBUG STREQUAL ON) 
	SET( CMAKE_BUILD_TYPE "Debug" )
	SET ( DEBUG TRUE)
	ADD_DEFINITIONS(-DDEBUG)
ENDIF (DEBUG STREQUAL ON )



IF (DEV STREQUAL ON) 
	SET ( FLEXIBLAS_DEFAULT_LIB_PATH "${CMAKE_BINARY_DIR}/netlib:${CMAKE_BINARY_DIR}/src:${CMAKE_INSTALL_FULL_LIBDIR}/flexiblas/:")
	SET ( CMAKE_INSTALL_FULL_SYSCONFDIR "${CMAKE_BINARY_DIR}")
	MESSAGE (WARNING "-------------------------------------------------------------------------------------------")
	MESSAGE (WARNING "- This is an development build and can not be install. Please set add -DDEV=OFF to cmake. -")
	MESSAGE (WARNING "-------------------------------------------------------------------------------------------")
ELSE()
	IF ( INTEGER8 STREQUAL ON) 
		SET ( FLEXIBLAS_DEFAULT_LIB_PATH "${CMAKE_INSTALL_FULL_LIBDIR}/flexiblas64/")
	ELSE()
		SET ( FLEXIBLAS_DEFAULT_LIB_PATH "${CMAKE_INSTALL_FULL_LIBDIR}/flexiblas/")
	ENDIF()
ENDIF() 


IF (NOT DEFINED ABI) 
	IF ( ${CMAKE_Fortran_COMPILER_ID} STREQUAL "Intel")
		SET(ABI "Intel" CACHE STRING "ABI of the Fortran Interface")
		
	ELSE()
		SET(USE_INTERFACE_INTEL FALSE) 
		SET(ABI "GNU" CACHE STRING "ABI of the Fortran Interface")
	ENDIF()
ELSE()
	IF (NOT ( ABI STREQUAL "GNU" OR ABI STREQUAL "Intel"))
		MESSAGE(FATAL_ERROR "ABI must be GNU or Intel")
	ENDIF()
ENDIF()
IF ( ABI STREQUAL "Intel") 
	SET(USE_INTERFACE_INTEL TRUE) 
ELSE ()
	SET(USE_INTERFACE_INTEL FALSE) 
ENDIF()
#IF (NOT SYSCONFDIR) 
#	SET(CMAKE_INSTALL_FULL_SYSCONFDIR "${CMAKE_INSTALL_PREFIX}/etc/")
#ENDIF()
IF (DEFINED FALLBACK) 
	SET (FALLBACK_NAME "${FALLBACK}")
ELSE()
	SET (FALLBACK_NAME FALSE) 
ENDIF()

SET ( INCLUDE_DIR ${INCLUDE_DIR} ${CMAKE_BINARY_DIR}/include ${CMAKE_SOURCE_DIR}/include ${CMAKE_BINARY_DIR} )
CONFIGURE_FILE(${CMAKE_SOURCE_DIR}/config.h.in ${CMAKE_BINARY_DIR}/include/flexiblas_config.h)
LIST(REMOVE_DUPLICATES INCLUDE_DIR)
IF (APPLE) 
	SET ( CMAKE_C_FLAGS "${CMAKE_C_FLAGS} -D_FILE_OFFSET_BITS=64 -fPIC") 
ELSE() 
	IF (WIN32) 
		SET ( CMAKE_C_FLAGS "${CMAKE_C_FLAGS} -D_FILE_OFFSET_BITS=64 -Wall")
	ELSE ()
		SET ( CMAKE_C_FLAGS "${CMAKE_C_FLAGS} -D_FILE_OFFSET_BITS=64 -rdynamic -Wall -fPIC ")
	ENDIF()
ENDIF()
SET ( CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -D_FILE_OFFSET_BITS=64 -fPIC") 

SET(CMAKE_INSTALL_RPATH_USE_LINK_PATH TRUE)


#
# Check Some files 
#
INCLUDE (CheckIncludeFiles)
IF (WIN32) 
	MESSAGE(STATUS "Building Windows Version" ) 
	IF (MINGW) 
		MESSAGE(STATUS "Using MingW Compiler")
	ENDIF() 
ELSE() 
	# CHECK_INCLUDE_FILES (stdatomic.h HAVE_STDATOMIC_H)
	CHECK_INCLUDE_FILES (dlfcn.h	 HAVE_DLFCN_H)

	INCLUDE (CheckSymbolExists) 
	CHECK_SYMBOL_EXISTS(RTLD_GLOBAL   "dlfcn.h" HAVE_RTLD_GLOBAL)
	CHECK_SYMBOL_EXISTS(RTLD_DEEPBIND "dlfcn.h" HAVE_RTLD_DEEPBIND)
	CHECK_SYMBOL_EXISTS(RTLD_NOW	  "dlfcn.h" HAVE_RTLD_NOW)

	INCLUDE (CheckFunctionExists) 
	CHECK_FUNCTION_EXISTS(dlopen DLOPEN_C) 
	SET(__CMAKE_REQUIRED_LIBRARIES ${CMAKE_REQUIRED_LIBRARIES})
	SET(CMAKE_REQUIRED_LIBRARIES dl)
	CHECK_FUNCTION_EXISTS(dlopen DLOPEN_DL)
	SET(CMAKE_REQUIRED_LIBRARIES ${__CMAKE_REQUIRED_LIBRARIES})

	IF (DLOPEN_DL) 
	 SET(LIBS ${LIBS} dl)
	ENDIF()
	IF (NOT ( DLOPEN_DL OR DLOPEN_C ) ) 
	 MESSAGE(ERROR "No dlopen function found")
	ENDIF()

	IF ( NOT HAVE_DLFCN_H ) 
		MESSAGE(FATAL_ERROR "dlfcn.h not found, aborting")
	ENDIF()
ENDIF()

INCLUDE(CheckAttributeExists)
Check_Attribute_Exists( constructor HAVE_ATTR_CONSTRUCTOR)
Check_Attribute_Exists( "alias(\"foo\")" HAVE_ATTR_ALIAS)

SET(EXTRA_BLAS "")

### libcscutils 
SET(CSCUTILS_FEATURES "inifile;file")
add_subdirectory(libcscutils)
SET(LIBS cscutils ${LIBS}) 
SET(INCLUDE_DIR ${INCLUDE_DIR} "${PROJECT_SOURCE_DIR}/libcscutils/include")

IF (CBLAS STREQUAL ON) 
	ADD_DEFINITIONS(-DFLEXIBLAS_CBLAS)
ENDIF()



### Setup Integer8 option
IF ( INTEGER8 STREQUAL ON ) 
	ADD_DEFINITIONS(-DINTEGER8)
	SET(FLEXIBLAS_INTEGER8 TRUE)
	SET(FLEXIBLAS_INCLUDE_PREFIX "flexiblas64") 
	SET(FLEXIBLAS_LIBRARY_DIR "flexiblas64")
	SET(FLEXIBLAS_PKG_CONFIG "flexiblas64.pc")
	set(flexiblasname flexiblas64)

ELSE()
	SET(FLEXIBLAS_INTEGER8 FALSE)
	SET(FLEXIBLAS_INCLUDE_PREFIX "flexiblas") 
	SET(FLEXIBLAS_PKG_CONFIG "flexiblas.pc") 
	SET(FLEXIBLAS_LIBRARY_DIR "flexiblas")
	set(flexiblasname flexiblas)

ENDIF (INTEGER8 STREQUAL ON) 


### Add Source 
add_subdirectory(netlib)
add_subdirectory(src)
add_subdirectory(examples)


### Install 
INSTALL(FILES   ${CMAKE_SOURCE_DIR}/include/blas_gnu.h
		${CMAKE_SOURCE_DIR}/include/blas_intel.h
		${CMAKE_SOURCE_DIR}/include/extblas_gnu.h
		${CMAKE_SOURCE_DIR}/include/extblas_intel.h
		${CMAKE_SOURCE_DIR}/include/cblas.h
		DESTINATION ${CMAKE_INSTALL_FULL_INCLUDEDIR}/${FLEXIBLAS_INCLUDE_PREFIX}/)
INSTALL(FILES   ${CMAKE_SOURCE_DIR}/src/flexiblas_backend.h 
		${CMAKE_SOURCE_DIR}/src/flexiblas_real_calls.h 
		${CMAKE_SOURCE_DIR}/src/flexiblas_real_extblas_calls.h 
		${CMAKE_SOURCE_DIR}/src/flexiblas_api.h
		${CMAKE_SOURCE_DIR}/src/flexiblas_dummy_fortran.h 
		${CMAKE_SOURCE_DIR}/src/flexiblas_dummy_cblas.h 
		DESTINATION ${CMAKE_INSTALL_FULL_INCLUDEDIR}/${FLEXIBLAS_INCLUDE_PREFIX}/) 

IF(CBLAS STREQUAL ON) 
	INSTALL(FILES	${CMAKE_SOURCE_DIR}/include/cblas.h DESTINATION ${CMAKE_INSTALL_FULL_INCLUDEDIR}/${FLEXIBLAS_INCLUDE_PREFIX}/)
ENDIF()


CONFIGURE_FILE(
  "${CMAKE_CURRENT_SOURCE_DIR}/cmake_uninstall.cmake.in"
  "${CMAKE_CURRENT_BINARY_DIR}/cmake_uninstall.cmake"
  IMMEDIATE @ONLY)
ADD_CUSTOM_TARGET(uninstall "${CMAKE_COMMAND}" -P "${CMAKE_CURRENT_BINARY_DIR}/cmake_uninstall.cmake") 

### Setup PKG_CONFIG
set(prefix ${CMAKE_INSTALL_PREFIX})
set(libdir ${CMAKE_INSTALL_FULL_LIBDIR})
set(PKG_CONFIG_DIR ${CMAKE_INSTALL_FULL_LIBDIR}/pkgconfig)
set(includedir ${CMAKE_INSTALL_PREFIX}/include/${FLEXIBLAS_INCLUDE_PREFIX}/)
CONFIGURE_FILE(${CMAKE_CURRENT_SOURCE_DIR}/flexiblas.pc.in ${CMAKE_CURRENT_BINARY_DIR}/${FLEXIBLAS_PKG_CONFIG})
install(FILES ${CMAKE_CURRENT_BINARY_DIR}/${FLEXIBLAS_PKG_CONFIG}  DESTINATION ${PKG_CONFIG_DIR} )

### Setup Man Pages 
install(DIRECTORY doc/man/ DESTINATION ${CMAKE_INSTALL_FULL_MANDIR} FILES_MATCHING PATTERN "*.[1-9]")


IF ( TESTS ) 
	MESSAGE( STATUS "Enable Testing...")
	ENABLE_TESTING()
	add_subdirectory(test)
ENDIF (TESTS)

#
# Final Status
#
MESSAGE(STATUS "------------------------------------------------")
MESSAGE(STATUS "      Final Configuration Status ")
MESSAGE(STATUS "------------------------------------------------")
MESSAGE(STATUS "Build 8 byte integer: ${INTEGER8}")
MESSAGE(STATUS "Build CBLAS interface: ${CBLAS}")
MESSAGE(STATUS "Install path: ${CMAKE_INSTALL_PREFIX}")
MESSAGE(STATUS "Extra BLAS search path: ${HBLAS_DEFAULT_LIB_PATH}")
MESSAGE(STATUS "ABI: ${ABI}")
MESSAGE(STATUS "Interface to Extra BLAS Libraries:")
FOREACH(EBLAS ${EXTRA_BLAS})
MESSAGE(STATUS " - ${EBLAS}")
ENDFOREACH()
MESSAGE(STATUS "------------------------------------------------")


